import fs

input := fs.read_file(__dir__ + "input.txt")
grid := input.split_lines().map((line) => line.iter().collect()).collect()

height := len(grid)
width := len(grid[0])

mutable start_col: int = 0
for (col in range(width)):
    if grid[0][col] == 'S':
        start_col = col
        break
    end
end

fun part1() -> int:
    mutable beams: list[(int, int)] = [(0, start_col)]
    mutable split_count: int = 0
    mutable processed_splitters: list[(int, int)] = []
    
    while len(beams) > 0:
        mutable new_beams: list[(int, int)] = []
        
        for ((row, col) in beams):
            next_row := row + 1
            
            if next_row >= height:
                continue
            end
            
            next_cell := grid[next_row][col]
            
            if next_cell == '^':
                splitter_pos := (next_row, col)
                
                mutable already_processed: bool = false
                for (pos in processed_splitters):
                    if pos[0] == next_row && pos[1] == col:
                        already_processed = true
                        break
                    end
                end
                
                if !already_processed:
                    split_count++
                    processed_splitters += [splitter_pos]
                    
                    if col > 0:
                        new_beams += [(next_row, col - 1)]
                    end
                    if col < width - 1:
                        new_beams += [(next_row, col + 1)]
                    end
                end
            else:
                new_beams += [(next_row, col)]
            end
        end
        
        beams = new_beams
    end
    return split_count
end

fun part2() -> int:
    mutable dp_curr: list[int] = array(width, 0)
    mutable dp_next: list[int] = array(width, 0)

    dp_curr[start_col] = 1

    for (r in range(height - 1)):
        for (c in range(width)):
            if dp_curr[c] == 0:
                continue
            end

            next_cell := grid[r + 1][c]

            if next_cell == '^':
                if c > 0:
                    dp_next[c - 1] += dp_curr[c]
                end
                if c < width - 1:
                    dp_next[c + 1] += dp_curr[c]
                end
            else:
                dp_next[c] += dp_curr[c]
            end
        end

        dp_curr = dp_next
        dp_next = array(width, 0)
    end

    mutable total: int = 0
    for (c in range(width)):
        total += dp_curr[c]
    end

    return total
end

print(f"Part 1: {part1()}")
print(f"Part 2: {part2()}")