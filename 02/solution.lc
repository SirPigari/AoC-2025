import fs
import (floor) from math

input := fs.read_file(__dir__ + "input.txt")
ranges: list[(int, int)] = input.split(',').map((it) => it.trim().split('-')).map((it) => (it[0] as int, it[1] as int)).collect()

mutable invalid_id_sum_p1: int = 0
mutable invalid_ids_p1: list[int] = []
mutable invalid_id_sum_p2: int = 0
mutable invalid_ids_p2: list[int] = []

// NOTE:
// i have been fucking with this for 2 hours because i missread the description
// its really this fucking simple
// i had a WHOLE FUCKING AUTOMATON HERE
fun is_valid_id_p1(id: int) -> bool:
    id_str := id as str
    n := len(id_str)

    if n % 2 == 1:
        return true
    end

    half := n / 2
    if id_str[0..half] == id_str[half..]:
        return false
    end

    return true
end

fun is_valid_id_p2(id: int) -> bool:
    id_str := id as str
    n := len(id_str)

    if n == 1:
        return true
    end

    max_l := floor(n / 2.0) as int
    for l in [1..max_l]:
        if n % l != 0:
            continue
        end

        seq := id_str[0..l]
        repeated := true

        for i in range(l, n, l):
            if (id_str[i..(i + l)] != seq):
                repeated := false
                break
            end
        end

        if repeated:
            return false
        end
    end

    return true
end


for ((start_id, end_id) in ranges):
    print(f"{start_id} - {end_id}")
    for id in [start_id..end_id]:
        if id % 100 == 0:
            print(f"Checking ID: {id}")
        end
        if !is_valid_id_p1(id):
            invalid_id_sum_p1 += id
            invalid_ids_p1 += [id]
        end
        if !is_valid_id_p2(id):
            invalid_id_sum_p2 += id
            invalid_ids_p2 += [id]
        end
    end
end

print("Invalid ID sum P1: " + invalid_id_sum_p1)
print("Invalid IDs P1: " + invalid_ids_p1)
print("Invalid ID sum P2: " + invalid_id_sum_p2)
print("Invalid IDs P2: " + invalid_ids_p2)
